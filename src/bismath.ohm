Bismath {

  Program     = Statement+
	
  Statement   = IfStmt                             
              | MathStmt       
              | Loop 
              | Function
              | Call
         
  Block      = "{" Statement* "}"                                     
                       
  MathStmt  = Type? id "=" Exp endline						              -- assignment
  				  | id "=" Exp endline                             	  -- assign
  				  | put Exp endline                                   -- print					
            | output Exp? endline                               -- return
            | getout endline                                    -- break    
       
  Exp       = ("-" | "!") Exp7                               -- unary
            | Exp1 "?" Exp1 ":" Exp                          -- ternary
            | Exp1       
  Exp1      = Exp1 "||" Exp2                                 -- binary
            | Exp2       
  Exp2      = Exp2 "&&" Exp3                                 -- binary
            | Exp3       
  Exp3      = Exp4 ("<="|"<"|"=="|"!="|">="|">") Exp4        -- binary
            | Exp4
  Exp4      = Exp4 ("+" | "-" | "|+|" | "|-|" | "<+>" | "<->") Exp5                          -- binary
            | Exp5       
  Exp5      = Exp5 ("*" | "/" | "%" | "|*|" | "|D|" | "|T|" | "<*>") Exp6                    -- binary
            | Exp6       
  Exp6      = Exp7 "^" Exp6                                 -- binary
            | Exp7       
  Exp7      = num       
            | true       
            | false
            | strlit
            | Lookup       
            | Call       
            | id
            | Array
            | Matrix  
            | Vector
            | Point     
            | "(" Exp ")"                                    -- parens

  Loop      = for id in Exp Block                                                       -- for  
            | while Exp Block                                                           -- while

  IfStmt    = "(" Exp ")" "->" Block otherwise (Block | IfStmt)   -- long
            | "(" Exp ")" "->" Block                              -- short
  
  Function  = function Type? id "(" Params ")" Block

  Call      = id "(" Args ")"
  Params    = ListOf<ParamId, ",">
  Args      = ListOf<PossibleArgs, ",">
  
  ParamId = Type? id
  PossibleArgs = Type? Exp

  Array     = "[" ListOf<Exp, ","> "]"
  Matrix    = "|" ListOf< ListOf<(num | id), ",">, ";" > "|"
  Vector    =  "<" ListOf<(num | id), ","> ">"
  Point     =  "(" num "," num ("," num)? ")"
  Lookup    = Exp7 "[" Exp "]"

  strlit    = "\"" char* "\""
  char      = ~control ~"\\" ~"\"" any
            | "\\" ("n" | "t" | "\"" | "\\")                --escape
            | "\\u{" hex hex? hex? hex? hex? hex? "}"       --codepoint
  control   = "\x00".."\x1f" | "\x80".."\x9f"
  hex       = hexDigit
  
  Type =  (StructTypes | NumTypes | GeneralTypes) 
  StructTypes = "[" (NumTypes | GeneralTypes) "]" | "|" NumTypes "|" | "<" NumTypes ">" | "(" NumTypes ")"
  NumTypes = "int32" | "int64" | "int" | "float32" | "float64" | "float"
  GeneralTypes = "bool" | "string"

  in     	  = "in"  ~idchar
  declare   = "declare" ~idchar
  function  = "function" ~idchar
  for 	  	= "for" ~ idchar
  while     = "while" ~idchar
  true      = "true" ~idchar
  false     = "false" ~idchar
  put       = "put" ~idchar
  getout    = "getout" ~idchar
  otherwise = "otherwise" ~idchar
  output    = "output" ~idchar
  keyword   = function | while | true | false | put | getout | otherwise | output | for | in
  num       = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?
  id        = ~keyword letter idchar*
  idchar    = letter | digit | "_"
  space    += "//" (~"\n" any)* ("\n" | end)                 -- comment
  newline     = "\n"+
  endline    = ";"
}
